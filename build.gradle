plugins {
    id "com.github.ben-manes.versions" version "0.17.0"
}

repositories {
    mavenCentral()
}


group 'org.example'
version '1.0-SNAPSHOT'


apply plugin: 'java'
apply plugin: 'idea'


sourceCompatibility = 1.8

ext.versions = [
    slf4j   : '1.7.25',
    log4j   : '2.10.0',
    jackson : '2.9.4',
    guava   : '24.0-jre',
    caffeine: '2.6.1',
    metrics : '4.0.2',
    junit   : '5.1.0',
    mockito : '2.15.0',
    assertj : '3.9.1',
]


dependencies {

    compile group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4j
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: versions.slf4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: versions.log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: versions.log4j

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: versions.jackson
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: versions.jackson

    compile group: 'com.google.guava', name: 'guava', version: versions.guava
    compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: versions.caffeine
    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: versions.metrics

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    testCompile group: 'org.mockito', name: 'mockito-core', version: versions.mockito
    testCompile group: 'org.assertj', name: 'assertj-core', version: versions.assertj

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

}


configurations.all {

    resolutionStrategy {

        // Replace commons logging by slf4j, if present
        dependencySubstitution {
            substitute module('commons-logging:commons-logging') with module("org.slf4j:jcl-over-slf4j:${versions.slf4j}")
        }

    }

}


dependencyUpdates {

    resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

}


test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
}
