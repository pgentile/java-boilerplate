// Sample build script, including most common libraries
//
// References :
//   - JUnit 5 and Gradle: https://github.com/junit-team/junit5-samples/tree/dev/junit5-gradle-consumer
//   - JUnit platform runner: http://junit.org/junit5/docs/current/user-guide/#running-tests-junit-platform-runner

buildscript {

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.netflix.nebula:gradle-lint-plugin:5.1.0'
    }

}


repositories {
    mavenCentral()
}


group 'org.example'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'nebula.lint'


sourceCompatibility = 1.8

ext.versions = [
    slf4j       : '1.7.21',
    log4j       : '2.6.2',
    jackson     : '2.8.2',
    guava       : '19.0',
    caffeine    : '2.3.3',
    metrics     : '3.1.2',
    junit       : '5.0.0-M2',
    junitVintage: '4.12.0-M2',
    mockito     : '2.1.0-beta.120',
    assertj     : '3.5.2',
]


dependencies {

    compile group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4j
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: versions.slf4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: versions.log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: versions.log4j

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: versions.jackson

    compile group: 'com.google.guava', name: 'guava', version: versions.guava
    compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: versions.caffeine
    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: versions.metrics

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    testRuntime group: 'org.junit.vintage', name: 'junit-vintage-engine', version: versions.junitVintage
    testCompile group: 'org.mockito', name: 'mockito-core', version: versions.mockito
    testCompile group: 'org.assertj', name: 'assertj-core', version: versions.assertj

}


configurations.all {

    resolutionStrategy {

        // Replace commons logging by slf4j, if present
        dependencySubstitution {
            substitute module('commons-logging:commons-logging') with module("org.slf4j:jcl-over-slf4j:${versions.slf4j}")
        }

    }

}
